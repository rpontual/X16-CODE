100 REM INIT
110 TS = 0.023012 : REM TI TO SECONDS FOUND EMPIRICALLY WITH R31
120 LX=48: CX=80: REM LX=LINES ON CANVAS, CX=COLUMNS ON CANVAS
130 DIM MX(LX+1,CX+1), B$(LX)
140 LE=0:CE=0 :REM LE=LINES ENTERED, CE=MAX COLUMNS ENTERED
150 GN=0:PN=0 :REM GN=GENERATION NUMBER, PN=POPULATION NUMBER
160 LO=8:CO=30 :REM ORGANISM ENTER LINE AND COLUMN
170 L3=1:L4=LX-1:C3=1:C4=CX-1: REM POINTERS TO ACCELERATE THE GENERATION UPDATE
180 L5=L3:L6=L4
190 GOSUB 3010: REM INITIALIZE THE CURSOR POSITIONING VARIABLE, USES X$

200 REM PREPARE SCREEN
210 PRINT CHR$(147);
220 R9=0:C9=0:B9=6:GOSUB 4000: REM X16 LOGO
230 PRINT:PRINT:PRINT TAB(38);"LIFE"
240 PRINT:PRINT TAB(25);"BASED ON CONWAY'S GAME OF LIFE"
270 X=59:Y=50:GOSUB 3100:PRINT "VERSION 0.1 BY ROMULO PONTUAL";

300 GOSUB 2000: REM ENTER ORGANISM
310 GOSUB 2400: REM LOAD ORGANISM AS BINARY INTO MX(N,N)
320 X=59:Y=0:GOSUB 3100: REM CURSOR POSITIONING
330 PRINT " ELAPSED TIME FROM LAST GENERATION IS           ";
340 GOSUB 2200: REM PRINT ORIGINAL ORGANISM

900 REM LOOP ==================================================================
910 SA = TI

1000 REM UPDATE GENERATION
1010 REM L AND C ARE USED FOR LINE AND COLUMN REFERENCE
1020 REM MX(N,N) ORGANISM, INTEGER 0 DEAD 1 LIVE, DECIMAL NUMBER OF NEIGHBOHRS
1030 L5=L3:L6=L4:C5=C3:C6=C4
1040 L3=LX:L4=1:C3=CX:C4=1: REM ARE TO LIMIT GENETIC LAWS RANGE
1050 GOSUB 2500: REM COUNT NEIGHBOHRS
1060 PN = 0
1070 GOSUB 2700: REM IMPLEMENTATION OF GENETIC LAWS
1080 GN=GN+1
1090 GOSUB 2200: REM PRINT GENERATION

1850 X=59:Y=37:GOSUB 3100
1860 PRINT INT((TI-SA)*TS);CHR$(157);" SECONDS ";:REM TIME TO UPDATE GENERATION
1870 IF PN<1 THEN PRINT "THIS IS THE END OF THE LIFE" : END
1880 GOTO 900
1890 REM LOOP =================================================================

2000 REMSUB ENTER ORGANISM
2010 X=LO  :Y=CO-10: GOSUB 3100
2011 PRINT "ENTER YOUR PATTERN, NO NEED TO CENTER IT"
2020 X=LO+1:Y=CO-18: GOSUB 3100
2021 PRINT "YOU MAY USE SPACE, ANY CHAR, DOWN ARROW. RETURN TO FINISH"
2025 POKE 204,0: REM GET A BLINKING CURSOR
2030 LE = 1: REM FIRST LINE OF ARRAY B$
2040 GET A$: IF A$="" THEN 2040: REM WAINTING CHARACTER INPUT
2050 IF ASC(A$)=13 THEN PRINT" ": GOTO 2110: REM USER PRESSED RETURN TO FINISH
2060 IF ASC(A$)=17 THEN LE=LE+1:PRINT" ": GOTO 2040: REM NEW LINE
2070 IF A$<>" " THEN A$=CHR$(209): REM ANY CHARACTER IS CONVERTED TO *
2080 PRINT A$;
2090 B$(LE)=B$(LE)+A$
2100 GOTO 2040
2110 POKE 204,1: REM STOP BLINKING CURSOR
2115 X=LO  :Y=CO-10: GOSUB 3100 
2116 PRINT "                                        "
2120 X=LO+1:Y=CO-18: GOSUB 3100
2121 PRINT "                                                         "
2130 RETURN

2200 REM PRINT THE GENERATION
2210 X=LO:Y=0:GOSUB 3100
2220 PRINT "GENERATION ="; GN;" ": PRINT "POPULATION ="; PN;" "
2230 FOR L=1 TO LX
2240 FOR C=1 TO CX
2245 IF L<L5-1 OR L>L6+1 THEN PRINT: GOTO 2280
2250 IF MX(L,C)<0.9 THEN PRINT ".";:GOTO 2270
2260 PRINT CHR$(209);
2270 NEXT C
2280 NEXT L
2290 RETURN

2400 REMSUB CONVERT ORGANISM INTO BINARY AND CENTERED ON MATRIX
2410 LS = (LX-LE)/2-1
2420 CS = (CX-CE)/2-1  
2430 FOR L=1 TO LE
2440 FOR C=1 TO CX
2450 PO$ = MID$(B$(L),C,1)
2460 IF PO$ <> " " AND PO$ <> "" THEN MX(L+LS,C+CS) = 1: PN=PN+1
2470 NEXT C
2480 NEXT L
2490 RETURN

2500 REM COUNT NEIGHBOHRS
2510 FOR L=L5-1 TO L6+1
2520 FOR C=C5-1 TO C6+1
2530 IF MX(L,C)<0.9 THEN 2650
2540 FOR NL=L-1 TO L+1 STEP 2
2550 FOR NC=C-1 TO C+1
2560 MX(NL,NC)=MX(NL,NC)+0.1
2570 NEXT NC
2580 NEXT NL
2590 MX(L,C-1)=MX(L,C-1)+0.1: REM NEED THESE TO MARK ALL 8 CELLS
2600 MX(L,C+1)=MX(L,C+1)+0.1
2610 IF L<L3 THEN L3=L
2620 IF L>L4 THEN L4=L
2630 IF C<C3 THEN C3=C
2640 IF C>C4 THEN C4=C
2650 NEXT C
2660 NEXT L
2670 RETURN

2700 REMSUB IMPLEMENTATION OF GENETIC LAWS
2710 FOR L=L3-1 TO L4+1
2720 FOR C=C3-1 TO C4+1
2730 IF MX(L,C) = 0 THEN 2770
2740 IF MX(L,C) > 0.25 AND MX(L,C) < 0.35 THEN MX(L,C)=1: PN=PN+1: GOTO 2770
2750 IF MX(L,C) < 1.15 OR MX(L,C) > 1.35 THEN MX(L,C)=0: GOTO 2770
2760 MX(L,C)=1: PN=PN+1
2770 NEXT C
2780 NEXT L
2790 RETURN

3000 REM TEXT SCREEN CURSOR POSITIONING
3010 REMSUB INITIALIAZTION (RUN ONCE)
3020 X$=""
3030 FOR I=0TO79:X$=X$+CHR$(17):NEXTI
3040 FOR I=0TO79:X$=X$+CHR$(29):NEXTI
3050 RETURN
3100 REMSUB LOCATE AT X LINE AND Y COLUMN
3110 PRINT CHR$(19);LEFT$(X$,X);RIGHT$(X$,Y);
3120 RETURN

4000 REMSUB X16 LOGO
4005 DIMH9(5)
4010 DATA $40,0,233,32,105,223,32,95,$E0,0,$30,0,$50,1,$70,0
4011 DATA 95,32,223,105,32,233,$A0,0,$20,0
4020 FORI=0TO6
4030 R8=R9+I:C8=I:IFI>3THENC8=6-I
4040 READX9,X8:X9=X9+B9
4050 IFI<1OR(I>3.5ANDI<4.5)THENFORK=0TO5:READH9(K):NEXT
4060 FORJ=0+X8TO2-X8
4070 VPOKE0,256*(R8)+2*(C9+C8+J),H9(J+3):VPOKE0,256*(R8)+2*(C9+C8+J)+1,X9
4071 VPOKE0,256*(R8)+2*(11+C9-C8-J)-2,H9(J):VPOKE0,256*(R8)+2*(11+C9-C8-J)-1,X9
4080 NEXT:NEXT
4090 RETURN
